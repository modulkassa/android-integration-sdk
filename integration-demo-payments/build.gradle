plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
}

Properties localProperties = new Properties()
File propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    localProperties.load(propertiesFile.newDataInputStream())
}

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId "ru.modulkassa.pos.integrationdemo.payments"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // For Kotlin projects
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release {
            storeFile file("../demo_keystore.jks")
            storePassword System.getProperty("android.storePassword") ?: localProperties.getProperty("android.storePassword")
            keyAlias System.getProperty("android.keyAlias") ?: localProperties.getProperty("android.keyAlias")
            keyPassword System.getProperty("android.keyPassword") ?: localProperties.getProperty("android.keyPassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':integration-library')

    implementation libraries.kotlin
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'

    testImplementation libraries.junit
}
