apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.jfrog.bintray'

def libVersionName = "0.14.0_MK-9766"
version = libVersionName
group 'ru.modulkassa.pos'

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionName libVersionName
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation libraries.kotlin
    implementation libraries.gson
    implementation libraries.timber

    testImplementation libraries.junit
    testImplementation libraries.robolectric
    testImplementation libraries.mockito
    testImplementation libraries.mockitoKotlin
    testImplementation libraries.kotlinJUnit
}

Properties properties = new Properties()
File propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    properties.load(propertiesFile.newDataInputStream())
}

publishing {

    repositories {
        maven {
            name 'Internal'
            url properties.getProperty("nexus.url")
            credentials {
                username properties.getProperty("nexus.username")
                password properties.getProperty("nexus.password")
            }
        }
    }

    publications {
        Bintray(MavenPublication) {
            groupId group
            artifactId 'integration-library'
            version libVersionName
            artifact("$buildDir/outputs/aar/integration-library-release.aar")
            artifact packageJavadoc
            artifact sourceJar

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.api.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task packageJavadoc(type: Jar, dependsOn: 'dokkaJavadoc') {
    from "$buildDir/javadoc"
    classifier = 'javadoc'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

artifacts {
    archives sourceJar, packageJavadoc
}

bintray {
    user = System.getenv('bintray_user')
    key = System.getenv('bintray_key')
    configurations = ['archives']
    pkg {
        repo = 'public'
        name = 'ru.modulkassa.integration'
        userOrg = 'modulkassa'
        licenses = ['Apache-2.0']
        desc = 'ModulKassa integration SDK'
        websiteUrl = "https://modulkassa.ru"
        publish = false
    }
    publications = ['Bintray']
}
